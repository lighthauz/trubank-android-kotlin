buildscript {
    ext.kotlin_version = '1.4.10'
    ext.koin_version = '2.1.5'
    ext.nav_version = "2.3.0"

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.28.1-alpha"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

ext.executeCmd = { cmd ->
    def process = cmd.execute()
    def output = new ByteArrayOutputStream()
    process.waitForProcessOutput(output, new ByteArrayOutputStream())

    if (process.exitValue() == 0) {
        output.toString().trim()
    } else {
        println "command: ${cmd} - (${process.exitValue()})"
    }
}

ext.environmentValue = { key ->
    System.getenv(key as String)?.trim() ?: null
}

ext.currentBranch = {
    environmentValue.call("GITHUB_HEAD_REF") ?: executeCmd.call("git rev-parse --abbrev-ref HEAD") ?: "master"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
        jcenter()
        maven {
            credentials {
                username = System.getenv("trusonaUsername")
                password = System.getenv("trusonaPassword")
            }
            url = "https://trusona.jfrog.io/trusona/sdk-releases/"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
