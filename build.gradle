buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext.executeCmd = { cmd ->
    def process = cmd.execute()
    def output = new ByteArrayOutputStream()
    process.waitForProcessOutput(output, new ByteArrayOutputStream())

    if (process.exitValue() == 0) {
        output.toString().trim()
    }
    else {
        println "command: ${cmd} - (${process.exitValue()})"
    }
}

ext.environmentValue = { key ->
    System.getenv(key as String)?.trim() ?: null
}

ext.currentBranch = {
    environmentValue.call("GITHUB_HEAD_REF") ?: executeCmd.call("git rev-parse --abbrev-ref HEAD") ?: "master"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        jcenter()

        maven {
            credentials {
                username = System.getenv("trusonaUsername")
                password = System.getenv("trusonaPassword")
            }
            url = "https://trusona.jfrog.io/trusona/sdk-releases/"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
